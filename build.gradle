version = "0.14.3"
group = 'ch.tutteli.kbox'

buildscript {
    ext {
        kotlin_version = '1.3.70'

        atrium_version = '0.9.2'
        spek_version = '2.0.10'

        // gradle plugins
        tutteli_plugins_version='0.32.2'
        detekt_version = '1.6.0'
    }

    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "ch.tutteli:tutteli-gradle-dokka:${tutteli_plugins_version}"
        classpath("ch.tutteli:tutteli-gradle-kotlin-module-info:$tutteli_plugins_version")
        classpath "ch.tutteli:tutteli-gradle-kotlin-utils:${tutteli_plugins_version}"
        classpath "ch.tutteli:tutteli-gradle-project-utils:${tutteli_plugins_version}"
        classpath "ch.tutteli:tutteli-gradle-publish:${tutteli_plugins_version}"
        classpath "ch.tutteli:tutteli-gradle-spek:${tutteli_plugins_version}"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detekt_version"
    }
}

apply plugin: 'ch.tutteli.project.utils'
apply plugin: 'ch.tutteli.kotlin.utils'
kotlinutils.kotlinVersion = kotlin_version

repositories {
    mavenCentral()
    jcenter()
}
apply plugin: "io.gitlab.arturbosch.detekt"
detekt {
    failFast = true
    input = files(subprojects*.collect { it.projectDir })
    config = files("$rootProject.projectDir/gradle/scripts/detekt.yml")
    reports {
        xml.enabled = true
    }
}

subprojects {
    repositories { jcenter() }
    description = 'A utility library for Kotlin'
}

configureCommonProjects()
configureJsProjects()
configureJvmProjects()
configureAndroidProjects()

def publishedProjects = subprojects

configure(getCommonProjects()) {
    dependencies {
        testImplementation "ch.tutteli.atrium:atrium-cc-en_GB-robstoll-common:$atrium_version", excluding {
            kotlin()
            kbox()
        }
        testImplementation "org.spekframework.spek2:spek-dsl-metadata:$spek_version"
    }
    //TODO delete (true is default) as soon as https://youtrack.jetbrains.com/issue/KT-30580 is fixed
    compileTestKotlinCommon {
        kotlinOptions.allWarningsAsErrors = false
    }
}
configure(getJsProjects()) {
    dependencies {
        testImplementation "ch.tutteli.atrium:atrium-cc-en_GB-robstoll-js:$atrium_version", excluding {
            kotlin()
            kbox()
        }
        testImplementation "org.spekframework.spek2:spek-dsl-js:$spek_version"
    }
}

configure(getJvmProjects()) {
    apply plugin: 'ch.tutteli.kotlin.module.info'
}

def jvmLikeProjects = getJvmProjects() + getAndroidProjects()
configure(jvmLikeProjects) { subproject ->
    apply plugin: 'ch.tutteli.spek'
    spek.version = spek_version

    dependencies {
        testImplementation "ch.tutteli.atrium:atrium-cc-en_GB-robstoll:$atrium_version", excluding {
            kotlin()
            kbox()
        }
        testImplementation "ch.tutteli.atrium:atrium-spec:$atrium_version", excluding {
            kotlin()
            kbox()
            exclude group: 'ch.tutteli.atrium'
        }
    }

    apply plugin: 'ch.tutteli.dokka'
}

configure(publishedProjects) { subproject ->
    apply plugin: 'ch.tutteli.publish'
}

/*

Release & deploy a commit
--------------------------------
1. search for X.Y.Z-SNAPSHOT and replace with X.Y.Z
2. update master:
    a) point to the tag
        1) search for `branch=master` and replace it with `tag=vX.Y.Z` (build status and coverage in README.md)
        2) search for `tree/master` and replace it with `tree/vX.Y.Z` (README.md)
    b) commit (modified .travis.yml, build.gradle, README.md)
    c) git tag vX.Y.Z
    d) git push origin vX.Y.Z
4. deploy to bintray:
    a) java -version 2>&1 | grep "version \"11" && CI=true ./gradlew clean publishToBintray
    b) Log in to bintray, check that there are 36 artifacts and publish them
    c) synchronise to maven central
5. create release on github

Prepare next dev cycle
-----------------------
1. point to master
   a) search for `tag=vX.Y.Z` and replace it with `branch=master`
   b) search for `tree/vX.Y.Z` and replace it with `tree/master`
2. search for X.Y.Z and replace with X.Y.Z-SNAPSHOT
3. commit & push changes

*/

