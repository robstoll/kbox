version = "0.16.0-SNAPSHOT"
group = 'ch.tutteli.kbox'

buildscript {
    ext {
        kotlin_version = '1.5.21'

        atrium_version = '0.16.0'
        spek_version = '2.0.15'

        // gradle plugins
        tutteli_plugins_version = '0.33.1'
        detekt_version = '1.17.1'
    }

    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "ch.tutteli:tutteli-gradle-dokka:${tutteli_plugins_version}"
        classpath("ch.tutteli:tutteli-gradle-kotlin-module-info:$tutteli_plugins_version")
        classpath "ch.tutteli:tutteli-gradle-kotlin-utils:${tutteli_plugins_version}"
        classpath "ch.tutteli:tutteli-gradle-project-utils:${tutteli_plugins_version}"
        classpath "ch.tutteli:tutteli-gradle-publish:${tutteli_plugins_version}"
        classpath "ch.tutteli:tutteli-gradle-spek:${tutteli_plugins_version}"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detekt_version"
    }
}

apply plugin: 'ch.tutteli.project.utils'
apply plugin: 'ch.tutteli.kotlin.utils'
kotlinutils.kotlinVersion = kotlin_version

repositories {
    mavenCentral()
    jcenter()
}
apply plugin: "io.gitlab.arturbosch.detekt"
detekt {
    failFast = true
    input = files(subprojects*.collect { it.projectDir })
    config = files("$rootProject.projectDir/gradle/scripts/detekt.yml")
    reports {
        xml.enabled = true
    }
}

configureCommonProjects()
configureJsProjects()
configureJvmProjects()
configureAndroidProjects()

configure(getJvmProjects()) {
    apply plugin: 'ch.tutteli.kotlin.module.info'

    apply plugin: 'ch.tutteli.spek'
    spek.version = spek_version
    apply plugin: 'ch.tutteli.dokka'

    junitjacoco {
        jacoco {
            toolVersion = '0.8.7'
        }
    }
}

subprojects { subproject ->
    repositories { jcenter() }
    apply plugin: 'ch.tutteli.publish'
}

/*

Release & deploy a commit
--------------------------------
1. search for X.Y.Z-SNAPSHOT and replace with X.Y.Z
2. update master:
    a) point to the tag, search for `tree/master` and replace it with `tree/vX.Y.Z` (README.md)
    b) commit (modified .travis.yml, build.gradle, README.md)
    c) git tag vX.Y.Z
    d) git push origin vX.Y.Z
4. deploy to bintray:
    a) java -version 2>&1 | grep "version \"9" && CI=true ./gradlew clean publishToBintray
    b) Log in to bintray, check that there are 26 artifacts and publish them
    c) synchronise to maven central
5. create release on github

Prepare next dev cycle
-----------------------
1. point to master, search for `tree/vX.Y.Z` and replace it with `tree/master`
2. Replace badges
3. search for X.Y.Z and replace with X.Y.Z-SNAPSHOT
4. commit & push changes

*/

